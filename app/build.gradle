apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

if (file('./token.gradle').exists()) {
    apply from: 'token.gradle'
}

android {
    compileSdkVersion 28
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.wa2c.android.medoly.plugin.action.lastfm"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 25
        versionName "2.5.0"

        buildConfigField("String", "K1", "\"\"")
        buildConfigField("String", "K2", "\"\"")
        buildConfigField("String", "T1", "\"\"")
        buildConfigField("String", "T2", "\"\"")

        multiDexEnabled true
    }
    buildTypes {
        debug {
            versionNameSuffix 'd'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
        }
    }

    signingConfigs {
        debug {
            storeFile file("../debug.keystore")
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }

    dataBinding {
        enabled = true
    }

    // output file
    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            def newName = "MedolyLastfmPlugin_${defaultConfig.versionName}"
            variant.outputs.all {
                // filename
                outputFileName = "${newName}.apk"
                // mapping
                variant.assembleProvider.get().doLast {
                    if ( variant.mappingFile != null && variant.mappingFile.exists() ) {
                        copy {
                            from variant.mappingFile
                            into outputFile.parent
                            rename { String fileName ->
                                "${newName}_mapping.txt"
                            }
                        }
                    }
                }
            }
        }
    }

}

repositories {
    google()
    jcenter()
    maven { url 'https://github.com/wa2c/medoly-library/raw/master/repository/' }
    maven { url 'https://github.com/wa2c/prefs/raw/master/repository/' }
}

dependencies {
    // Medoly library
    implementation 'com.wa2c.android:medolylibrary:1.2.7'
    // Prefs
    implementation 'com.wa2c.android:prefs:0.1.2'

    // Support Library
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.preference:preference:1.1.1"
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    // Kotlin
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-android', version: '1.3.5'
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.3.5'

    // Firebase
    implementation 'com.google.firebase:firebase-core:17.4.2'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0'

    // https://mvnrepository.com/artifact/com.jakewharton.timber/timber
    implementation group: 'com.jakewharton.timber', name: 'timber', version: '4.7.1'
    // https://mvnrepository.com/artifact/com.pranavpandey.android/dynamic-toasts
    implementation group: 'com.pranavpandey.android', name: 'dynamic-toasts', version: '3.2.0'
    // https://mvnrepository.com/artifact/de.u-mass/lastfm-java
    //implementation group: 'de.u-mass', name: 'lastfm-java', version: '0.1.2'
    //implementation files('libs/lastfm-java-0.1.2-fix.jar')
    implementation files('libs/lastfm-kotlin-0.1.4-SNAPSHOT.jar')

    implementation 'com.thelittlefireman:AppKillerManager:2.1.1'
}
